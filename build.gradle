plugins {
    id "net.minecraftforge.gradle.forge" version "6f5327"
    id "org.spongepowered.mixin" version "d5f9873d60"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "java"
}

version = "2.0"
group = "mynameisjeff.simpletogglesprint"
archivesBaseName = "SimpleToggleSprint"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += "-Delementa.dev=true"
    clientJvmArgs += "-Delementa.debug=true"
    clientRunArgs += "--mixin mixins.simpletogglesprint.json"
    clientRunArgs += "--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker"
}

repositories {
    mavenCentral()
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url "https://repo.sk1er.club/repository/maven-public/" }
    maven { url "https://jitpack.io/" }
}

configurations {
    bundle
    implementation.extendsFrom(bundle)
}

dependencies {
    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")
    bundle "gg.essential:loader-launchwrapper:1.0.2"
    implementation "gg.essential:Essential:1194-10809-SNAPSHOT"
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.simpletogglesprint.refmap.json"
}

jar {

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.simpletogglesprint.json",
            "ModSide": "CLIENT",
            "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
            "TweakOrder": "0",
    )
    enabled = false
}

shadowJar {
    archiveFileName = jar.archiveFileName
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.bundle]

    exclude "**/module-info.class"
    exclude "dummyThing"
    exclude "modcore.jar"
}

sourceSets {
    main {
        output.resourcesDir = file("${buildDir}/classes/kotlin/main")
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}


reobf {
    shadowJar {}
}

sourceCompatibility = targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.reobfJar.dependsOn(shadowJar)